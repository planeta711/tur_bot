#подгрузили файл с именами модулей, библиотек и справочников
require: requirements.sc

#========================================= СТАРТ И ГЛОБАЛЬНЫЕ ИНТЕНТЫ ==================================================
theme: /  

    #старт диалога с интентов start и приветствия
    state: Welcome
        q!: (*start/старт)
        q!: * Мария *
        q!: $greeting
        script: $session = {}      //обнулили переменные сессии
    #проверяем есть ли имя клиента, представляемся соотв.образом и идем в Меню или Имя
        if: $client.name
            a: Здравствуйте, {{$client.name}}. Это Мария, бот Just Tour.
        else:
            a: Здравствуйте, я Мария, бот туристической компании Just Tour.
        go!: /Menu/Begin

    #глобальный ответ на нераспознанные реплики, контекст не меняется
    state: CatchAll || noContext = true 
        event!: noMatch
        random:
            a: Простите, я Вас не поняла. 
            a: Извините, я Вас не понимаю. Вы сказали: {{$request.query}}.
        random:
            a: Попробуйте ответить по-другому.
            a: Переформулируйте, пожалуйста, свой вопрос.

    #глобальный ответ на прощание
    state: Bye || noContext = true 
        q: $goodbye
        a: Всего доброго!

    #глобальный стейт отказа или отмены
    state: Cancel
        q!: $refuse
        q!: $cancel
        a: Поняла, выхожу из диалога.
        go!: /Exit
    
    #глобальный стейт при выходе из сценария по желанию клиента
    state: Exit
        a: Буду на связи. Обращайтесь, если понадоблюсь!
        script: $session = {}      //обнулили переменные сессии

    #глобальный стейт при завершении сценария после отправки заявки на тур
    state: End
        script: $session = {} //обнулили переменные сессии
        a: До свидания!

#====================================================== МЕНЮ ===========================================================

theme: /Menu
    state: Begin
        a: Я расcказываю о погоде в разных городах мира и могу оформить заявку на подбор тура. 
        go!: /Menu/Choose
    #предложили выбор; ответ про погоду поймает входной интент Погоды
    state: Choose
        a: Что Вас интересует?
        buttons:
            "Рассказать о погоде"
            "Оформить заявку на тур"
    #интент Что еще умеешь - идем в начало выбора       
        state: WhatElse
            q: $whatelse
            a: Я больше ничего не умею. Только рассказывать о погоде и оформлять заявку на тур.
            go!: /Menu/Choose
    #интент Отказ - идем на выход
        state: Deny
            q: $refuse
            q: ( * (не надо)/(не хочу)/(не буду) * )
            q: $no
            a: Как скажете.
            go!: /Exit
    #интент Прогноз погоды    
        state: Weather
            intent: /Погода
            go!: /Weather/Begin
    #интент Оформить заявку
        state: Tour
            q: $tour
            q: * (~тур) *
            a: В какой город или страну хотите поехать?
            buttons:
                "Еще не решил"
    #назван город или страна
            state: CityOrCountry
                q:  * $City * 
                q:  * $Country * 
    #запоминаем город или страну и их координаты и идем на начало Заявки
                script: getLocation ($parseTree)
                        $session.tripPlace = $session.place
                        delete $session.place
                a: Записала, {{$session.tripPlace.name}}
                go!: /Trip/Begin
    #не решил или нужна консультация - идем на начало Заявки
            state: NoSure
                q: $dontknow
                q: * ~консультация *
                a: Не проблема. Заполним заявку, а менеджер поможет Вам выбрать направление
                go!: /Trip/Begin
    #все остальные ответы
            state: NoMatch
                event: noMatch
                a: Я Вас не поняла. Давайте заполним заявку, а направление выберете потом
                go!: /Trip/Begin    
            